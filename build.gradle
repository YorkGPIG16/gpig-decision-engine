group 'gpig.group2'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://m2.j6mes.co/"
    }
    maven {
        url "http://clojars.org/repo/"
    }
}

dependencies {
    compile 'gpig.group2:layer-services:1.4.43'
    compile 'gpig.group2.models:drone-comms-model:1.2.25'


    compile 'org.clojars.ndimiduk:geodesy:1.1.1'
    compile 'de.grundid.opendatalab:geojson-jackson:1.6'
    compile 'co.j6mes.infra.srf:service-registration-framework:1.0.1.8'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'org.apache.httpcomponents:fluent-hc:4.5.2'

    compile group: 'math.geom2d', name: 'javaGeom', version: '0.11.1'
}



jar {
    manifest {
        attributes 'Implementation-Title': 'Decision Engine',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}



task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task runFinalJar(type: JavaExec) {
    classpath = files('build/libs/service-registration-framework-1.0.0-SNAPSHOT.jar')
    classpath += sourceSets.main.runtimeClasspath
    main = "co.j6mes.infra.srf.daemon.ServiceREgistrationDaemo"
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Decision Enginer')
                root.appendNode('description', 'A decision engine')
                root.appendNode('url', 'https://github.com/j6mes/gpig-de')
                root.appendNode('inceptionYear', '2016')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/j6mes/gpig-de')
                scm.appendNode('connection', 'scm:https://j6mes@github.com/j6mes/gpig-de.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/j6mes/gpig-de.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def j6mes = developers.appendNode('developer')
                j6mes.appendNode('id', 'j6mes')
                j6mes.appendNode('name', 'James Thorne')
                j6mes.appendNode('email', 'james@jthorne.co.uk')
            }
        }
    }
}

